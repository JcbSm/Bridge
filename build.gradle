plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "de.undercouch.download" version "5.5.0"
}

group 'github.jcbsm'
version '1.1'

repositories {
    mavenCentral()
    maven {
        name "papermc-repo"
        url "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name 'm2-dv8tion'
        url 'https://m2.dv8tion.net/releases'
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    implementation ('net.dv8tion:JDA:5.0.0-beta.11') {
        exclude module: 'opus-java'
    }
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'org.xerial:sqlite-jdbc:3.42.0.0'
    implementation 'club.minnced:discord-webhooks:0.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testImplementation 'io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    compileOnly "io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT"
}

tasks {

    jar {
        finalizedBy("shadowJar")
        archiveFileName.set(project.name + "-" + archiveVersion.get() + "-original.jar")
    }

    shadowJar {
        archiveFileName.set(project.name + "-" + archiveVersion.get() + ".jar")
        archiveClassifier.set("shaded")

        mustRunAfter("build")


    }
}

test {
    useJUnitPlatform()
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

jar {
    manifest {
        attributes
    }
}


processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks.register('downloadServerJar') {
    group 'server'
    download.run {
        src 'https://api.papermc.io/v2/projects/paper/versions/1.20.4/builds/399/downloads/paper-1.20.4-399.jar'
        dest project.property("testSrvDir") + "server.jar"
        onlyIfModified true
    }
}

tasks.register('copyJar') {
    group 'server'
    dependsOn 'shadowJar'
    doLast {
        copy {
            from 'build/libs/' + rootProject.name + '-' + project.version + '.jar'
            into project.property("testSrvDir") + "plugins"
        }
    }
}

tasks.register('run') {
    group 'server'
    dependsOn 'copyJar', 'downloadServerJar'
    doLast {
        exec {
            workingDir "${projectDir}" + project.property("testSrvDir")
            commandLine 'java', '-jar', 'server.jar', '-nogui'
        }
    }
}

compileJava.options.compilerArgs = ['-Xlint:unchecked', '-Xlint:deprecation']