plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group 'github.jcbsm'
version '0.0_alpha'

repositories {
    mavenCentral()
    maven {
        name "papermc-repo"
        url "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name 'm2-dv8tion'
        url 'https://m2.dv8tion.net/releases'
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    implementation ('net.dv8tion:JDA:5.0.0-beta.11') {
        exclude module: 'opus-java'
    }
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    compileOnly "io.papermc.paper:paper-api:1.19.2-R0.1-SNAPSHOT"
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

jar {
    manifest {
        attributes
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveClassifier = ''
}

tasks.register('jarIntoServer') {
    group 'server'
    dependsOn 'shadowJar'
    doLast {
        copy {
            from 'build/libs/' + rootProject.name + '-' + project.version + '.jar'
            into 'testsrv/plugins'
        }
    }
}

tasks.register('runServer') {
    group 'server'
    doLast {
        exec {
            workingDir "${projectDir}/testsrv/"
            commandLine 'java', '-jar', 'paper-1.19.2.jar', '-nogui'
        }
    }
}


tasks.register('buildRunServer') {
    group 'server'
    dependsOn 'jarIntoServer', 'runServer'
}

compileJava.options.compilerArgs = ['-Xlint:unchecked', '-Xlint:deprecation']